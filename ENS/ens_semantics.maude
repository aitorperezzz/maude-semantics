load ens_config.maude .

mod ENS-SEMANTICS is

	protecting ENS-SYNTAX .
	protecting ENS-UTILS .
	
	vars M M' : Memory .
	vars R R' R'' : RegName .
	var b : Bank .
	vars x x' y y' C n : Nat .
	vars P P' : Prog .

	*** Define the evaluation of a program in a certain State
	sort ProgEval .
	op exec(_,_) : Prog State -> ProgEval .
	


	*** Rewrite rules for arithmetic instructions

	rl [add] : exec( P { C ; ADD R R', R'' } P' , [ b ; M ; x ; y ; C ] ) => 
                exec( P { C ; ADD R R', R'' } P' , [ b[R'' / ( b[R] + b[R'] )] ; M ; 
                isneg ( b[R] + b[R'] ) ; iszero ( b[R] + b[R'] ) ; s(C) ] ) .

	rl [sub] : exec( P { C ; SUB R R', R'' } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; SUB R R', R'' } P' , [ b[R'' / ( b[R] - b[R'] )] ; M ; 
		 isneg ( b[R] - b[R'] ) ; iszero ( b[R] - b[R'] ) ; s(C) ] ) .

	rl [addinmed] : exec( P { C ; ADDI R n, R'' } P' , [ b ; M ; x ; y ; C ] ) => 
	        exec( P { C ; ADDI R n, R'' } P' , [ b[R'' / ( b[R] + n )] ; M ; 
	        isneg ( b[R] + n ) ; iszero ( b[R] + n ) ; s(C) ] ) .
	rl [subinmed] : exec( P { C ; SUBI R n, R'' } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; SUBI R n, R'' } P' , [ b[R'' / ( b[R] - n )] ; M ; 
		 isneg ( b[R] - n ) ; iszero ( b[R] - n ) ; s(C) ] ) .

        
        
        *** Rewrite rules for information exchange instructions
	rl [load] : exec( P { C ; LOAD n (R), R' } P' , [ b ; M ; x ; y ; C ] ) => 
	        exec( P { C ; LOAD n (R), R' } P' , [ b[R' /  M[ n + b[R] ] ] ; M ; 
	        isneg ( M[ n + b[R] ] ) ; iszero ( M[ n + b[R] ] ) ; s(C) ] ) .
	rl [store] : exec( P { C ; STORE R, n (R') } P' , [ b ; M ; x ; y ; C ] ) => 
	        exec( P { C ; STORE R, n (R') } P' , [ b ; M[(n + b[R']) / b[R] ] ; 
	        x ; y ; s(C) ] ) .



	*** Rewrite rules for jump instructions
	crl [jumpiflesstrue] : exec( P { C ; BL n } P' , [ b ; M ; x ; y ; C ] ) => 
	        exec( P { C ; BL n } P' , [ b ; M ; x ; y ; n ] ) 
	        if x == 1 .
	crl [jumpiflessfalse] : exec( P { C ; BL n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BL n } P' , [ b ; M ; x ; y ; s(C) ] ) 
		if x =/= 1  .

	crl [jumpifgreatertrue] : exec( P { C ; BG n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BG n } P' , [ b ; M ; x ; y ; n ] ) 
		if x == 0 and y == 0 .
	crl [jumpifgreaterfalse] : exec( P { C ; BG n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BG n } P' , [ b ; M ; x ; y ; s(C) ] ) 
		if not( x == 0 and y == 0 ) .

	crl [jumpifequaltrue] : exec( P { C ; BEQ n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BEQ n } P' , [ b ; M ; x ; y ; n ] ) 
		if y == 1 .
	crl [jumpifequalfalse] : exec( P { C ; BEQ n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BEQ n } P' , [ b ; M ; x ; y ; s(C) ] ) 
		if y =/= 1 .

	crl [jumpifdifftrue] : exec( P { C ; BNE n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BNE n } P' , [ b ; M ; x ; y ; n ] ) 
		if y == 0 .
	crl [jumpifdifffalse] : exec( P { C ; BNE n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BNE n } P' , [ b ; M ; x ; y ; s(C) ] ) 
		if y =/= 0 .

	crl [jumpifleqtrue] : exec( P { C ; BLE n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BLE n } P' , [ b ; M ; x ; y ; n ] ) 
		if x == 1 or y == 1 .
	crl [jumpifleqfalse] : exec( P { C ; BLE n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BLE n } P' , [ b ; M ; x ; y ; s(C) ] ) 
		if not( x == 1 or y == 1 ) .

	crl [jumpifgeqtrue] : exec( P { C ; BGE n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BGE n } P' , [ b ; M ; x ; y ; n ] ) 
		if x == 0 or y == 1 .
	crl [jumpifgeqfalse] : exec( P { C ; BGE n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BGE n } P' , [ b ; M ; x ; y ; s(C) ] ) 
		if not( x == 0 or y == 1 ) .

	rl [jump] : exec( P { C ; BR n } P' , [ b ; M ; x ; y ; C ] ) => 
		 exec( P { C ; BR n } P' , [ b ; M ; x ; y ; n ] ) .


	
endm
