fmod GREP-SYNTAX is

	protecting STRING .

	*** This GREP-like command will search if certain string matches an element of 
	*** the language specified by a regular expression. Strings (and characters) 
	*** are regular expressions by themselves
	sort RegExp .
	subsort String < RegExp .
	
	
	*** Epsilon is a regular expression, and by definition will match the empty string
	op Epsilon : -> RegExp [ctor] .


	*** Syntax of regular expressions

	*** SUM operator
	op _r+_ : RegExp RegExp -> RegExp [ctor assoc comm prec 27] .

	*** CONCATENATION operator
	op _r._ :  RegExp RegExp -> RegExp [ctor assoc prec 26] .

	*** STAR, or Kleene star
	op _r* : RegExp -> RegExp [ctor prec 25] .


	*** A Prog will recieve a regular expression and a string where is has to find matches
	sort Prog .
	op search {_} in {_} : RegExp String -> Prog .

endfm

fmod GREP-UTILS is

	protecting NAT .
	protecting STRING .

	*** This module contains functions that will split a given string into its possible
	*** divisions in two strings.
	sorts Sep SepList .
	subsort Sep < SepList .
	op EmptySep : -> Sep [ctor] .
	op {_,_} : String String -> Sep [ctor] .
	op __ : SepList SepList -> SepList [ctor assoc id: EmptySep] .

	
	*** divisions is a function that will accept a natural number l and a string and will give
	*** the division of the string where the first element has length l, and then will apply
	*** divisions to s(l) and the same string w, giving the division where the first string 
	*** has length l + 1, etc.


	op divisions(_,_) : Nat String -> SepList [ctor] .
	var w : String .
	var l : Nat .
	ceq divisions( l, w ) = { substr( w, 0, l ) , substr( w, l, sd( length(w), l ) ) } 
		divisions( s(l), w ) if l < length(w) .
	ceq divisions( l, w ) = { w, "" } if l == length(w) .

	*** Operator frontsplit gives all the possible splits of a string w, 
	*** without the possibility of { "", w }
	op frontsplit_ : String -> SepList [ctor] .
	eq frontsplit w = divisions( 1, w ) .

	*** The operator split is the same as frontsplit except it adds at the front 
	*** the possibility of { "", w }
	op split_ : String -> SepList [ctor] .
	eq split w = { "", w } frontsplit w .

endfm
