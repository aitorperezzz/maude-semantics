load imp_utils.maude .

fmod IMP-BASIC-SYNTAX is

	*** This module specifies the basic syntax.

	protecting IMP-STACK .

	*** Syntax of arithmetic expressions.
	sort AritExp .
	subsort Var < AritExp .
	subsort Int < AritExp .
	op _+_ : AritExp AritExp -> AritExp [ditto] .
	op _-_ : AritExp AritExp -> AritExp [ditto] .
	op _*_ : AritExp AritExp -> AritExp [ditto] .

	*** Syntax of boolean expresions.
	sort BoolExp .
	ops true false : -> BoolExp [ctor] .
	op _==_ : AritExp AritExp -> BoolExp [comm prec 26] .
	op _!=_ : AritExp AritExp -> BoolExp [comm prec 26] .
	op _leq_ : AritExp AritExp -> BoolExp .
	op _geq_ : AritExp AritExp -> BoolExp .
	op not_ : BoolExp -> BoolExp .
	op _and_ : BoolExp BoolExp -> BoolExp [assoc comm] .
	op _or_ : BoolExp BoolExp -> BoolExp [assoc comm] .

	*** Syntax of statements.
	sort Prog .
	op skip : -> Prog [ctor] .
	op _=_ : Var AritExp -> Prog [ctor prec 30] .
	op _;_ : Prog Prog -> Prog [ctor assoc] .
	op if_then_else_ : BoolExp Prog Prog -> Prog [ctor] .
	op while_do_ : BoolExp Prog -> Prog [ctor] .

endfm

fmod IMP-FUNCTIONS-SYNTAX is

	*** This module specifies the syntax of function calls.
	
	protecting IMP-BASIC-SYNTAX .

	*** Define lists of variables for the definition of a function.
	sort VarList .
	subsort Var < VarList .
	op void : -> VarList [ctor] .
	op _,_ : VarList VarList -> VarList [ctor assoc id: void] .

	*** Define lists of arithmetic expressions for the function calls from scripts.
	sort AritExpList .
	subsort AritExp < AritExpList .
	subsort VarList < AritExpList .
	op _,_ : AritExpList AritExpList -> AritExpList [ctor assoc id: void] .
	
	*** Define the Func type.
	sorts FuncName Func .
	subsort Qid < FuncName .

	*** Specify how to declare a function.
	op function _ = _ (_) {_} : Var FuncName VarList Prog -> Func [ctor] .
	
	*** Specify how to call a function.
	op _(_) : FuncName AritExpList -> AritExp [ctor] . 

endfm

fmod IMP-OBJECTS-SYNTAX is
	
	*** This module specifies the syntax of classes, to be able to create objects.

	protecting IMP-FUNCTIONS-SYNTAX .
	
	sort Class ClassName ClassVar ClassFunc .
	subsort Qid < ClassName .
	subsort ClassVar < AritExp .
	***subsort Func < ClassFunc .

	op _=_ : ClassVar AritExp -> Prog [ditto] .

	*** Define the object's variables.
	op _._ : ClassName Var -> ClassVar [ctor] .
	op this ._ : Var -> ClassVar [ctor] .

	*** Define the object's functions.
	sort ClassFuncList . 
	subsort ClassFunc < ClassFuncList .
	op EmptyClassFuncList : -> ClassFuncList [ctor] .
	op __ : ClassFuncList ClassFuncList -> ClassFuncList [ctor assoc id: EmptyClassFuncList] .

	*** Define thes syntax to write a class.
	op class _ {_} : ClassName ClassFuncList -> Class [ctor] .

	*** Define the syntax to create a new instance of an object.
	op _ = new _(_) : Var ClassName AritExpList -> Prog [ctor] .

	*** A special function in a class is its constructor function.
	op constructor(_) {_} : VarList Prog -> ClassFunc [ctor] .

	*** Define what a list of numbers is. Will be useful for the semantics of 
	*** running the constructor function.
	sort NumberList .
	subsort Int < NumberList .
	subsort NumberList < AritExpList .
	op _,_ : NumberList NumberList -> NumberList [ctor assoc id: void] .

endfm

fmod IMP-MEMORY is

	*** This module specifies the memory, which will consist of a stack for the global
	*** variables, and then a reserved stack for each object declared.

	protecting IMP-OBJECTS-SYNTAX .

	var c : ClassName .
	var v : Var .
	var M : Memory .
	***var z : ClassVar .
	var S : Stack .
	vars N N' : Int .

	sorts Memory MainMemory ClassMemory ThisMemory .
	subsort MainMemory < Memory .
	subsort ClassMemory < Memory .
	subsort ThisMemory < Memory .
	op EmptyMemory : -> Memory [ctor] .
	op [main] _ : Stack -> MainMemory [ctor] .
	op [this] _ : Stack -> ThisMemory [ctor] .
	op [_] _ : ClassName Stack -> ClassMemory [ctor] .
	op __ : Memory Memory -> Memory [ctor assoc comm id: EmptyMemory] .


	*** Access elements in memory.
	*** Global variables.
	op _[_] : Memory Var ~> Int [ctor] .
	eq M [ v ] = main M [ v ] .
	*** Object variables
	op _[_] : Memory ClassVar ~> Int [ctor] .
	eq M [ c . v ] = object M [ c . v ] .
	eq M [ this . v ] = this M [ this . v ] .

	*** Change or add elements to memory.
	*** Global variables.
	op _[_/_] : Memory Var Int -> Memory [ctor] .
	eq M [ v / N ] = main M [ v / N ] .
	*** Object variables.
	op _[_/_] : Memory ClassVar Int -> Memory [ctor] .
	eq M [ ( c . v ) / N ] = object M [ ( c . v ) / N ] .
	eq M [ ( this . v ) / N ] = this M [ ( this . v ) / N ] .

	*** Access elements in memory, specific definitions.
	*** Case of global variables.
	op main _[_] : Memory Var ~> Int [ctor] .
	eq main (M [main] ( S < v, N > ) ) [v] = N .
	*** Case of class variables from the outside.
	op object _[_] : Memory ClassVar ~> Int [ctor] .
	eq object (M [c] ( S < v, N > ) ) [ ( c . v ) ] = N .
	*** Case of class variables from the inside.
	op this _[_] : Memory ClassVar ~> Int [ctor] .
	eq this (M [this] ( S < v, N > ) ) [ this . v ] = N .

	*** Change values or add elements to memory, specific definitions.
	*** Case of global variables.
	op main _[_/_] : Memory Var Int -> Memory .
	eq main (M [main] ( S < v, N >) ) [v / N'] = M [main] ( S < v, N' > ) .
	eq main (M [main] S) [v / N] = M [main] ( S < v, N > ) [owise] .
	*** Case of class variables from the outside.
	op object _[_/_] : Memory ClassVar Int -> Memory .
	eq object (M [c] ( S < v, N > ) ) [ ( c . v )  / N'] = M [c] ( S < v, N' > ) .
	eq object (M [c] S) [ c . v / N] = M [c] ( S < v, N > ) [owise] .
	*** Case of class variables from the inside.
	op this _[_/_] : Memory ClassVar Int -> Memory .
	eq this (M [this] ( S < v, N > ) ) [ ( this . v )  / N'] = M [this] ( S < v, N' > ) .
	eq this (M [this] S) [ ( this . v ) / N] = M [this] ( S < v, N > ) [owise] .

endfm

fmod IMP-EXTRAS-SYNTAX is
	
	*** This module contains the Extra type specification (a function or a class can be extras).

	protecting IMP-MEMORY .

	vars v b : Var .
	var vl : VarList .
	var al : AritExpList .
	var N : Int .
	var mem : Memory .

	sorts Extra ExtraList .
	subsort Extra < ExtraList .
	subsort Func < Extra .
	subsort Class < Extra .

	*** Combine definitions of functions and classes.
	op EmptyExtra : -> ExtraList [ctor] .
	op __ : ExtraList ExtraList -> ExtraList [ctor assoc comm id: EmptyExtra] .

	*** Define the type Script, a Prog type followed by, possibly, a list of Extra types.
	sort Script .
	op main{_} : Prog -> Script [ctor] .
	op main{_}_ : Prog ExtraList -> Script [ctor] .

	*** Define the syntax to give each function its own memory when called.
	op giveStack [___] : VarList AritExpList Memory -> Stack [ctor] .
	eq giveStack [ ( v, vl ) ( b, al ) mem ] = < v, mem [ b ] > giveStack [ vl al mem ] .
	eq giveStack [ ( v, vl ) ( N, al ) mem ] = < v, N > giveStack [ vl al mem ] .
	eq giveStack [ ( void ) ( void ) mem ] = EmptyStack .

endfm






